#show.
#show init/2.

#program step(t).
% output only for planning robot as indicated by planning(robot(R))
#show occurs(object(robot,R), action(move,D),     t) :    move(robot(R),D,t), planning(robot(R)).
#show occurs(object(robot,R), action(pickup,()),  t) :  pickup(robot(R),_,t), planning(robot(R)).
#show occurs(object(robot,R), action(putdown,()), t) : putdown(robot(R),_,t), planning(robot(R)).

#show occurs(object(robot,R), action(deliver,(O,A  )), t) : process(product(A),order(O),C,t), toShelf(S,C), position(robot(R),C,t-1), planning(robot(R)).
#show occurs(object(robot,R), action(deliver,(O,A  )), t) : process(product(A),order(O),C,t), atShelf(S,C,t-1), carries(robot(R),S,t-1), planning(robot(R)).
#show occurs(object(robot,R), action(deliver,(O,A  )), t) : process(product(A),order(O),C,t), position(S,C,t), carries(robot(R),S,t-1), shelved(S,product(A)), planning(robot(R)).
#show occurs(object(robot,R), action(deliver,(O,A,N)), t) : process(product(A),order(O),S,N,t), carries(robot(R),S,t-1), planning(robot(R)).

% additional output in working format to be used as input for the next robot
% again only for planning robot
#show move(robot(R),D,t)    : move(robot(R),D,t), planning(robot(R)).
#show pickup(robot(R),S,t)  : pickup(robot(R),S,t), planning(robot(R)).
#show putdown(robot(R),S,t) : putdown(robot(R),S,t), planning(robot(R)).
% position and carry atom are needed for when the plan length of the next robot is below this plan length
% in all other cases they are not needed as they would be generated by rules in action-MPP.lp
#show position(R,C,t)       : position(R,C,t), planning(R).
#show position(S,C,t)       : position(S,C,t), planning(R), carries(R,S,_).
#show carries(R,S,t)        : carries(R,S,t), planning(R).
